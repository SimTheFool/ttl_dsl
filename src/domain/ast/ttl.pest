WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }

ALPHAFIRST = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
ALPHATEXT = @{ ASCII_ALPHANUMERIC* }
identifier = @{ ALPHAFIRST }

reference = @{ identifier }
number = @{ ASCII_DIGIT+ }
string = { "\"" ~ (ALPHATEXT) ~ "\"" }

object_element = { import | declaration }
object = { "{" ~ object_element* ~ "}" }

meta = { number | string | reference }
metas = { "[" ~ meta* ~ "]" }
value = { number | string | object | reference }

declaration = {metas? ~ identifier ~ ":" ~ value }

path = @{ ("." | "/" | ALPHAFIRST)+ }
import = { "<<" ~ path ~ ("with" ~ declaration)* }

transform_layer = { identifier }
transform_rule = @{ (!NEWLINE ~ ANY)* }
transform = ${ "@TRANSFORM" ~ WHITESPACE ~ transform_layer ~ (NEWLINE ~ WHITESPACE* ~ ">" ~ " "* ~ transform_rule)* }

file = { SOI ~ value ~ transform* ~ EOI }