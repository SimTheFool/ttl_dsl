WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }
ALPHAFIRST = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
identifier = @{ (ASCII_ALPHANUMERIC | CASED_LETTER | " " | "_")*}


reference = @{ "$" ~ ALPHAFIRST }
number = @{ ASCII_DIGIT+ }
TEXTCONTENT = @{!(NEWLINE | "$" | "@") ~ (!NEWLINE ~ ANY)+ }
text = { TEXTCONTENT }
STRINGCONTENT = @{ (!"\"" ~ ANY)* }
string = { "\"" ~ STRINGCONTENT ~ "\"" }

object_element = { import | declaration }
object = { "{" ~ object_element* ~ "}" }

meta = { number | string | reference }
metas = { "[" ~ meta* ~ "]" }

declare_direct_mark = @{":"}
declare_uniq_mark = @{"!"}
declare_mark = { (declare_direct_mark | declare_uniq_mark) }
declaration = {metas? ~ identifier ~ declare_mark ~ value}

value = { number | object | reference | string | text }

import_id = @{ ("." | "/" | ALPHAFIRST)+ }
import_variable = {identifier ~ ":" ~ value }
import_config = { "with" ~ (import_variable | import) }

import_anon_mark = {"<" ~ identifier? ~ "?"}
import_named_mark = {"<" ~ identifier? ~ "@"}
import_uniq_mark = {"<" ~ identifier? ~ "!"}
import_mark = { (import_anon_mark | import_named_mark | import_uniq_mark) }
import = { import_mark ~ import_id ~ import_config* ~ ">" }

transform_layer = { identifier }
transform_rule = @{ (!NEWLINE ~ ANY)* }
transform = ${ "@TRANSFORM" ~ WHITESPACE ~ transform_layer ~ (NEWLINE ~ WHITESPACE* ~ ">" ~ " "* ~ transform_rule)* }

name = { "@NAME" ~ identifier }

file = { SOI ~ name? ~ value? ~ transform* ~ EOI }