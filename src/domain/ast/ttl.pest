WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }

ALPHAFIRST = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
ALPHATEXT = @{ ASCII_ALPHANUMERIC* }

identifier = @{ ALPHAFIRST }
reference = @{ identifier }
number = @{ ASCII_DIGIT+ }
string = { "\"" ~ (ALPHATEXT) ~ "\"" }

object_element = { import | declaration }
object = { "{" ~ object_element* ~ "}" }

meta = { number | string | reference }
metas = { "[" ~ meta* ~ "]" }
declaration = {metas? ~ identifier ~ ":" ~ value }

value = { number | string | object | reference }

import_id = @{ ("." | "/" | ALPHAFIRST)+ }
import_variable = {identifier ~ ":" ~ value }
import_config = { "with" ~ (import_variable | import) }

import_anon_mark = {"<<"}
import_named_mark = {"<@"}
import_uniq_mark = {"<!"}
import_mark = { (import_anon_mark | import_named_mark | import_uniq_mark) }
import = { import_mark ~ import_id ~ import_config* ~ ">" }

transform_layer = { identifier }
transform_rule = @{ (!NEWLINE ~ ANY)* }
transform = ${ "@TRANSFORM" ~ WHITESPACE ~ transform_layer ~ (NEWLINE ~ WHITESPACE* ~ ">" ~ " "* ~ transform_rule)* }

name = { "@NAME" ~ identifier }

file = { SOI ~ name? ~ value ~ transform* ~ EOI }